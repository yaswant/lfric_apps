!-----------------------------------------------------------------------------
! (C) Crown copyright 2024 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!> @page Miniapp ngarch program
!> @brief Main program used to test physics schemes for NGARCH
!> @details Calls init and final from gungho with a simplified step from the
!>          ngarch_driver_mod
program ngarch

  use cli_mod,                     only : get_initial_filename
  use driver_collections_mod,      only : init_collections, final_collections
  use constants_mod,               only : precision_real
  use driver_comm_mod,             only : init_comm, final_comm
  use driver_config_mod,           only : init_config, final_config
  use driver_log_mod,              only : init_logger, final_logger
  use gungho_modeldb_mod,          only : modeldb_type
  use driver_time_mod,             only : init_time, final_time
  use log_mod,                     only : log_event,       &
                                          log_level_trace, &
                                          log_scratch_space
  use mpi_mod,                     only : global_mpi

  use ngarch_mod,        only : ngarch_required_namelists
  use ngarch_driver_mod, only : step
  use gungho_driver_mod, only : initialise, finalise

  implicit none

  ! The technical and scientific state
  type( modeldb_type )      :: modeldb
  character(*), parameter   :: application_name = "ngarch"
  character(:), allocatable :: filename

  call modeldb%configuration%initialise( application_name, table_len=10 )
  call modeldb%values%initialise( 'values', 5 )

  ! Create the field collections in modeldb
  call modeldb%fields%add_empty_field_collection( "depository", &
                                                  table_len = 100 )
  call modeldb%fields%add_empty_field_collection( "prognostic_fields", &
                                                  table_len = 100 )
  call modeldb%fields%add_empty_field_collection( "diagnostic_fields", &
                                                  table_len = 100 )

  call modeldb%io_contexts%initialise(application_name, 100)


  modeldb%mpi => global_mpi
  call init_comm( application_name, modeldb%mpi )
  call get_initial_filename( filename )
  call init_config( filename,                            &
                    ngarch_required_namelists, &
                    modeldb%configuration )
  deallocate( filename )

  call init_logger( modeldb%mpi%get_comm(), application_name )
  call init_collections()
  call init_time( modeldb%clock, modeldb%calendar )

  call log_event( 'Initialising ' // application_name // ' ...', log_level_trace )
  call initialise( application_name, modeldb )

  do while (modeldb%clock%tick())
    call step( application_name, modeldb )
  end do

  call log_event( 'Finalising ' // application_name // ' ...', log_level_trace )
  call finalise( application_name, modeldb )
  call final_time( modeldb%clock, modeldb%calendar )
  call final_collections()
  call final_logger( application_name )
  call final_config()
  call final_comm( modeldb%mpi )

end program ngarch
