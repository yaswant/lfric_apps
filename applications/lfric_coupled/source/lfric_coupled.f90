!-----------------------------------------------------------------------------
! (C) Crown copyright 2017-2024 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> This is a code that uses the LFRic infrastructure to build a model that
!> includes the GungHo dynamical core and physics parametrisation schemes
!> that are currently provided through the use of unified model code.

!> @brief Main program used to illustrate an atmospheric model built using
!>        LFRic infrastructure

!> @details This top-level code simply calls initialise, run and finalise
!>          routines that are required to run the atmospheric model.

program lfric_coupled

  use cli_mod,                only : get_initial_filename
  use coupler_mod,            only : set_cpl_name
  use driver_collections_mod, only : init_collections, final_collections
  use driver_comm_mod,        only : init_comm, final_comm
  use driver_config_mod,      only : init_config, final_config
  use driver_log_mod,         only : init_logger, final_logger
  use driver_time_mod,        only : init_time, final_time
  use gungho_mod,             only : gungho_required_namelists
  use gungho_driver_mod,      only : initialise, step, finalise
  use driver_modeldb_mod,     only : modeldb_type
  use lfric_mpi_mod,          only : global_mpi

  implicit none

  ! Model run working data set
  type(modeldb_type) :: modeldb

  character(*), parameter :: application_name = "lfric_coupled"
  character(*), parameter :: cpl_component_name = "lfric"

  character(:), allocatable :: filename

  modeldb%mpi => global_mpi

  call modeldb%configuration%initialise( application_name, table_len=10 )

  call modeldb%values%initialise( 'values', 5 )

  ! Create the depository, prognostics and diagnostics field collections
  call modeldb%fields%add_empty_field_collection("depository", table_len = 100)
  call modeldb%fields%add_empty_field_collection("prognostic_fields",         &
                                                  table_len = 100)
  call modeldb%fields%add_empty_field_collection("diagnostic_fields",         &
                                                  table_len = 100)
  call modeldb%fields%add_empty_field_collection("lbc_fields",                &
                                                    table_len = 100)
  call modeldb%fields%add_empty_field_collection("radiation_fields",          &
                                                    table_len = 100)
  call modeldb%fields%add_empty_field_collection("ls_fields",                 &
                                                    table_len = 100)
  call modeldb%fields%add_empty_field_collection("fd_fields",                 &
                                                    table_len = 100)

  call modeldb%io_contexts%initialise(application_name, 100)

  call set_cpl_name(modeldb, cpl_component_name)
  call init_comm( application_name, modeldb )
  call get_initial_filename( filename )
  call init_config( filename, gungho_required_namelists, &
                    modeldb%configuration )
  call init_logger( modeldb%mpi%get_comm(), application_name )
  call init_collections()
  call init_time( modeldb )
  deallocate(filename)

  call initialise( application_name, modeldb )
  do while (modeldb%clock%tick())
    call step( modeldb )
  end do
  call finalise( application_name, modeldb )

  call final_time( modeldb )
  call final_collections()
  call final_logger( application_name )
  call final_config()
  call final_comm( modeldb )

end program lfric_coupled
