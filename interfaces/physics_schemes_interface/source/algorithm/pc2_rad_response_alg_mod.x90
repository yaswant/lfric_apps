!-------------------------------------------------------------------------------
! (c) Crown copyright 2017 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-------------------------------------------------------------------------------
!> @brief Interface to the PC2 cloud scheme

module pc2_rad_response_alg_mod

   use constants_mod,        only: r_def
   use field_mod,            only: field_type
   use field_collection_mod, only: field_collection_type
   use io_config_mod,        only: write_diag, use_xios_io

   implicit none

   private
   public pc2_rad_response_alg

contains
  !>@brief Run pc2_rad_response process
  !>@details PC2 homogeneous forcing and turbulence
  !>         1) Forcing to theta (not T), mv, mcl and pressure are considered.
  !>         2) Turbulence part is redundant as subsequent kernel
  !>            calls pc2_rad_response with zeros for mixing rates.
  !>         3) Fields do not get updated. Increments are passed back out.
  !>         See UMDP30 for full scheme details.
  !>@param[in]     mr_n           Start of timestep mixing ratios
  !>@param[in]     theta          Potential temperature (theta) in wth space
  !>@param[in]     derived_fields Group of derived fields
  !>@param[in]     cloud_fields   Group of cloud fields
  !>@param[in]     dtheta_forcing Potential temperature forcing (not T) in wth space
  !>@param[out]    dtheta_pc2_inc Potential temperature response in wth space
  !>@param[in,out] dmv_pc2_inc    Vapour                response in wth space
  !>@param[out]    dmcl_pc2_inc   Liquid water content  response in wth space
  !>@param[out]    dcfl_pc2_inc   Liquid cloud fraction response in wth space
  !>@param[out]    dbcf_pc2_inc   Bulk cloud fraction   response in wth space
  !> @param[in]    dt             The model timestep length

  subroutine pc2_rad_response_alg( mr_n,           &
                                   theta,          &
                                   derived_fields, &
                                   cloud_fields,   &
                                   dtheta_forcing, &
                                   dtheta_pc2_inc, &
                                   dmv_pc2_inc,    &
                                   dmcl_pc2_inc,   &
                                   dcfl_pc2_inc,   &
                                   dbcf_pc2_inc, dt )

    use pc2_homogeneous_kernel_mod, only: pc2_homogeneous_kernel_type
    use mr_indices_mod,             only: imr_v, imr_cl, imr_ci, nummr
    use timing_mod,                 only: start_timing, stop_timing, tik, LPROF

    implicit none

    type( field_type ), intent( in ) :: mr_n ( nummr )
    type( field_type ), intent( in ) :: theta
    type( field_type ), intent( in ) :: dtheta_forcing
    type( field_type )               :: dmv_forcing
    type( field_type )               :: dmcl_forcing

    type( field_collection_type ), intent(in) :: derived_fields, cloud_fields

    ! Increments out.
    type( field_type ), intent( out ) :: dtheta_pc2_inc
    type( field_type ), intent( inout ) :: dmv_pc2_inc
    type( field_type ), intent( out ) :: dmcl_pc2_inc
    type( field_type ), intent( out ) :: dcfl_pc2_inc
    type( field_type ), intent( out ) :: dbcf_pc2_inc
    real( kind=r_def ), intent( in )  :: dt

    ! Temporary unpacked fields from collections
    type( field_type ), pointer :: exner_wth  => null()
    ! Dummy, as will be set to same as above
    type( field_type ), pointer :: exner_copy_wth  => null()

    ! For PC2
    type( field_type ), pointer :: liquid_fraction      => null()
    type( field_type ), pointer :: frozen_fraction      => null()
    type( field_type ), pointer :: bulk_fraction        => null()
    integer( tik )              :: id_alg, id_diags

    if ( LPROF ) call start_timing( id_alg, 'cloud.pc2_radiation' )

    ! Unpack fields
    call cloud_fields%get_field('liquid_fraction', liquid_fraction)
    call cloud_fields%get_field('frozen_fraction', frozen_fraction)
    call cloud_fields%get_field('bulk_fraction', bulk_fraction)

    call derived_fields%get_field('exner_in_wth', exner_wth)
    call derived_fields%get_field('exner_in_wth', exner_copy_wth)

    call theta%copy_field_properties(dmv_forcing)
    call theta%copy_field_properties(dmcl_forcing)
    call theta%copy_field_properties(dtheta_pc2_inc)
    call theta%copy_field_properties(dmcl_pc2_inc)
    call theta%copy_field_properties(dcfl_pc2_inc)
    call theta%copy_field_properties(dbcf_pc2_inc)

    call invoke( setval_c(dmv_forcing,    0.0_r_def),                 &
                 setval_c(dmcl_forcing,   0.0_r_def),                 &
                                                                      !
                 pc2_homogeneous_kernel_type ( mr_n(imr_v),           &
                                               mr_n(imr_cl),          &
                                               liquid_fraction,       &
                                               frozen_fraction,       &
                                               bulk_fraction,         &
                                               theta,                 &
                                               exner_wth,             &
                                               exner_copy_wth,        &
                                                 ! Forcings
                                               dtheta_forcing,        &
                                               dmv_forcing,           &
                                               dmcl_forcing,          &
                                                 ! Response increments
                                               dtheta_pc2_inc,        &
                                               dmv_pc2_inc,           &
                                               dmcl_pc2_inc,          &
                                               dcfl_pc2_inc,          &
                                               dbcf_pc2_inc,          &
                                               dt ) )

    ! N.B. We are not updating the variables that came in, just
    !      providing increments that need to be added on later.

    if ( LPROF ) call stop_timing( id_alg, 'cloud.pc2_radiation' )

    if ( write_diag .and. use_xios_io ) then
      if ( LPROF ) call start_timing( id_diags, 'diags.pc2_radiation' )
      call dtheta_pc2_inc%write_field('cloud__dtheta_pc2_rad')
      call dmv_pc2_inc%write_field('cloud__dmv_pc2_rad')
      call dmcl_pc2_inc%write_field('cloud__dmcl_pc2_rad')
      call dcfl_pc2_inc%write_field('cloud__dcfl_pc2_rad')
      call dbcf_pc2_inc%write_field('cloud__dbcf_pc2_rad')
      if ( LPROF ) call stop_timing( id_diags, 'diags.pc2_radiation' )
    end if

  end subroutine pc2_rad_response_alg

end module pc2_rad_response_alg_mod
