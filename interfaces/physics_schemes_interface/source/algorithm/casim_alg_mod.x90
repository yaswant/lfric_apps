!-------------------------------------------------------------------------------
! (c) Crown copyright 2023 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-------------------------------------------------------------------------------
!> @brief Interface to the CASIM Microphysics scheme

module casim_alg_mod

  use field_mod,            only: field_type
  use field_collection_mod, only: field_collection_type
  ! xios output
  use io_config_mod,        only: write_diag, use_xios_io
  use constants_mod,        only: r_def, i_def

  use sci_geometric_constants_mod, only: get_height_fv, get_dz_at_wtheta
  use fs_continuity_mod,           only: W3, Wtheta
  use mesh_mod,                    only: mesh_type

  use casim_diagnostics_mod,   only: initialise_diags_for_casim,               &
                                     output_diags_for_casim


  implicit none

  private
  public casim_alg

contains
  !>@brief Run the CASIM Microphysics scheme
  !>@details The CASIM Microphysics scheme calculates:
  !>             1) Precipitation rates output to the surface
  !>                and other physics schemes.
  !>             2) Increments to the large scale prognostics
  !>                due to cloud microphysical processes
  !>                (e.g. latent heating and cooling).
  !>         See UMDP50 for full scheme details
  !>@param[in]     mr_n           Start of timestep mixing ratios
  !>@param[in]     theta          Potential temperature (theta) in wth space
  !>@param[in]     dry_rho_in_w3  Dry density in the w3 space
  !>@param[in]     derived_fields Group of derived fields
  !>@param[in,out] microphysics_fields    Fields for mphys scheme
  !>@param[in]     cloud_fields           Fields for cloud scheme
  !>@param[in]     aerosol_fields         Fields for the aerosol schemes
  !>@param[in]     turbulence_fields      Fields for turbulence scheme
  !>@param[in]     mesh                   Current mesh
  !>@param[in,out] dmr_mphys      Mixing ratio increment due to microphysics
  !>@param[in,out] theta_inc      Potential temperature increment in wth space
  !>@param[in,out] dcfl           Increment to liquid cloud fraction
  !>@param[in,out] dcff           Increment to ice cloud fraction
  !>@param[in,out] dbcf           Increment to bulk cloud fraction
  subroutine casim_alg( mr_n, theta, dry_rho_in_w3, derived_fields,            &
                        microphysics_fields, cloud_fields, aerosol_fields,     &
                        turbulence_fields, mesh,                               &
                        dmr_mphys, theta_inc, dcfl, dcff, dbcf )

    use casim_kernel_mod, only: casim_kernel_type
    use mphys_turb_gen_kernel_mod, only: mphys_turb_gen_kernel_type
    use mr_indices_mod,   only: imr_v, imr_cl, imr_r, imr_ci, imr_s, imr_g, nummr
    use microphysics_config_mod, only: turb_gen_mixph
    use um_sizes_init_mod,       only: um_sizes_init

    use timing_mod,                only: start_timing, stop_timing, tik, LPROF
    use log_mod,                   only: log_event, LOG_LEVEL_INFO

    implicit none

    type( field_type ), intent( in )    :: mr_n ( nummr ), theta, dry_rho_in_w3

    type( field_collection_type ), intent(in)    :: derived_fields
    type( field_collection_type ), intent(in)    :: microphysics_fields
    type( field_collection_type ), intent(in)    :: cloud_fields
    type( field_collection_type ), intent(in)    :: aerosol_fields
    type( field_collection_type ), intent(in)    :: turbulence_fields

    type( field_type ), intent( inout ) :: dmr_mphys ( nummr )
    type( field_type ), intent( inout ) :: theta_inc
    type( field_type ), intent( inout ) :: dcfl, dcff, dbcf

    type(mesh_type), intent(in), pointer :: mesh

    ! Temporary unpacked fields from collections
    type( field_type ), pointer :: exner_in_wth        => null()
    type( field_type ), pointer :: w_in_wth           => null()
    type( field_type ), pointer :: wetrho_in_w3        => null()
    type( field_type ), pointer :: rho_in_wth          => null()

    type( field_type ), pointer :: bcf_in
    type( field_type ), pointer :: cfl_in              => null()
    type( field_type ), pointer :: cff_in              => null()

    type( field_type ), pointer :: dmv_mphys           => null()
    type( field_type ), pointer :: ls_rain             => null()
    type( field_type ), pointer :: ls_snow             => null()
    type( field_type ), pointer :: ls_graup            => null()
    type( field_type ), pointer :: lsca_2d             => null()

    type( field_type ), pointer :: ls_rain_3d          => null()
    type( field_type ), pointer :: ls_snow_3d          => null()

    type( field_type ), pointer :: nl_mphys            => null()
    type( field_type ), pointer :: nr_mphys            => null()
    type( field_type ), pointer :: ni_mphys            => null()
    type( field_type ), pointer :: ns_mphys            => null()
    type( field_type ), pointer :: ng_mphys            => null()

    ! In cloud value passed to radiation
    type( field_type ), pointer :: cloud_drop_no_conc  => null()
    type( field_type ), pointer :: murk                => null()

    type( field_type ), pointer :: wvar

    type( field_type ), pointer :: height_w3 => null()
    type( field_type ), pointer :: height_wth => null()
    type( field_type ), pointer :: dz_in_wth

    type( field_type ) :: refl_tot
    type( field_type ) :: refl_1km
    type( field_type ) :: superc_liq
    type( field_type ) :: superc_rain
    type( field_type ) :: ls_graup_3d

    integer(kind=i_def) :: mesh_id, ncells
    integer(tik)        :: id_alg, id_diags

    if ( LPROF ) call start_timing( id_alg, 'microphysics.casim' )

    call log_event( 'slow_physics: Running Microphysics', LOG_LEVEL_INFO )

    mesh_id = theta%get_mesh_id()

    call theta%copy_field_properties(dcfl)
    call theta%copy_field_properties(dcff)
    call theta%copy_field_properties(dbcf)
    call invoke( setval_c(dcfl,      0.0_r_def), &
                 setval_c(dcff,      0.0_r_def), &
                 setval_c(dbcf,      0.0_r_def), &
                 setval_c(theta_inc, 0.0_r_def) )

    ! Unpack fields
    call derived_fields%get_field('exner_in_wth', exner_in_wth)
    call derived_fields%get_field('w_in_wth', w_in_wth)
    call derived_fields%get_field('wetrho_in_w3', wetrho_in_w3)
    call derived_fields%get_field('rho_in_wth', rho_in_wth)

    call cloud_fields%get_field('bulk_fraction', bcf_in)
    call cloud_fields%get_field('liquid_fraction', cfl_in)
    call cloud_fields%get_field('frozen_fraction', cff_in)

    call microphysics_fields%get_field('ls_rain', ls_rain)
    call microphysics_fields%get_field('ls_snow', ls_snow)
    call microphysics_fields%get_field('ls_graup', ls_graup)

    call microphysics_fields%get_field('lsca_2d', lsca_2d)

    call microphysics_fields%get_field('ls_rain_3d', ls_rain_3d)
    call microphysics_fields%get_field('ls_snow_3d', ls_snow_3d)

    call microphysics_fields%get_field('nl_mphys',nl_mphys)
    call microphysics_fields%get_field('nr_mphys',nr_mphys)
    call microphysics_fields%get_field('ni_mphys',ni_mphys)
    call microphysics_fields%get_field('ns_mphys',ns_mphys)
    call microphysics_fields%get_field('ng_mphys',ng_mphys)

    call aerosol_fields%get_field('cloud_drop_no_conc',cloud_drop_no_conc)
    call aerosol_fields%get_field('murk', murk)

    call turbulence_fields%get_field('wvar', wvar)

    height_w3 => get_height_fv(W3, mesh_id)
    height_wth => get_height_fv(Wtheta, mesh_id)

    call initialise_diags_for_casim( refl_tot, refl_1km, superc_liq,           &
                                     superc_rain, ls_graup_3d )

    call invoke(name = "invoke_casim_kernel_type",                             &
                 casim_kernel_type ( mr_n(imr_v), mr_n(imr_cl), mr_n(imr_ci),  &
                                     mr_n(imr_r), mr_n(imr_g), mr_n(imr_s),    &
                                     cfl_in, cff_in,                           &
                                     nl_mphys, nr_mphys,                       &
                                     ni_mphys, ns_mphys, ng_mphys,             &
                                     w_in_wth,                                 &
                                     theta, exner_in_wth,                      &
                                     wetrho_in_w3, dry_rho_in_w3,              &
                                     height_w3, height_wth,                    &
                                     dmr_mphys(imr_v),  dmr_mphys(imr_cl),     &
                                     dmr_mphys(imr_ci), dmr_mphys(imr_r),      &
                                     dmr_mphys(imr_g), dmr_mphys(imr_s),       &
                                     ls_rain, ls_snow, ls_graup, lsca_2d,      &
                                     ls_rain_3d, ls_snow_3d, ls_graup_3d,      &
                                     theta_inc, cloud_drop_no_conc, murk,      &
                                     refl_tot, refl_1km,                       &
                                     superc_liq, superc_rain ) )

    if (turb_gen_mixph) then
      dz_in_wth => get_dz_at_wtheta( mesh_id )
      ! Switch UM to running i-first on whole domain
      ncells = mesh%get_last_edge_cell()
      call um_sizes_init(ncells)
      call invoke( mphys_turb_gen_kernel_type(theta, mr_n(imr_v), mr_n(imr_cl),&
                                              mr_n(imr_ci), mr_n(imr_s),       &
                                              ns_mphys, ni_mphys,              &
                                              cfl_in, cff_in, bcf_in,          &
                                              exner_in_wth, rho_in_wth, wvar,  &
                                              dz_in_wth,                       &
                                              theta_inc, dmr_mphys(imr_v),     &
                                              dmr_mphys(imr_cl),               &
                                              dmr_mphys(imr_ci),               &
                                              dmr_mphys(imr_s),                &
                                              dcfl, dcff, dbcf) )
      ! Switch UM back to columns
      call um_sizes_init(1_i_def)
    end if

    ! Save mv increment for stochastic physics
    call microphysics_fields%get_field('dmv_mphys', dmv_mphys)
    call invoke(setval_X(dmv_mphys, dmr_mphys(imr_v)))

    if ( LPROF ) call stop_timing( id_alg, 'microphysics.casim' )

    ! Output microphysics diagnostics
    if (write_diag .and. use_xios_io) then

      if ( LPROF ) call start_timing( id_diags, 'diags.microphysics' )
        call output_diags_for_casim(ls_rain, ls_snow, ls_graup,               &
                                    lsca_2d, refl_tot, refl_1km,              &
                                    ls_rain_3d, ls_snow_3d, ls_graup_3d,      &
                                    nl_mphys, nr_mphys, ni_mphys, ns_mphys,   &
                                    ng_mphys, exner_in_wth, theta_inc,        &
                                    dmr_mphys(imr_v),  dmr_mphys(imr_cl),     &
                                    dmr_mphys(imr_ci), dmr_mphys(imr_r),      &
                                    dmr_mphys(imr_g), dmr_mphys(imr_s),       &
                                    superc_liq, superc_rain )

      if ( LPROF ) call stop_timing( id_diags, 'diags.microphysics' )
    end if

  end subroutine casim_alg

end module casim_alg_mod
