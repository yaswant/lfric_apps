!-------------------------------------------------------------------------------
! (c) Crown copyright 2018 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-------------------------------------------------------------------------------
!> @brief Interface to the UM Lambert-Lewis convection scheme

module conv_ll_alg_mod

  use constants_mod,        only: i_def
  use field_mod,            only: field_type
  use field_collection_mod, only: field_collection_type
  use mr_indices_mod,       only: nummr, imr_v, imr_cl
  use timestepping_config_mod, only: outer_iterations
  use timing_mod,           only: start_timing, stop_timing, tik, LPROF
  ! xios output
  use io_config_mod,        only: write_diag, use_xios_io
  use log_mod,              only: log_event, LOG_LEVEL_INFO

  implicit none

  private
  public conv_ll_alg

contains

  !> @brief Run the Lambert-Lewis convection scheme
  !> @details The Lambert-Lewis convection scheme is a simple
  !>           convection parametrization that mixes theta and moisture
  !>           as documented in UMDP41
  !> @param[in,out] mr             Mixing ratios
  !> @param[in]     exner          Exner pressure field in density (w3) space
  !> @param[in]     derived_fields Group of derived fields
  !> @param[in,out] convection_fields Fields for convection scheme
  !> @param[in]     outer          Outer loop counter
  subroutine conv_ll_alg(mr, exner, derived_fields, convection_fields,    &
                         outer)

    use conv_ll_kernel_mod, only: conv_ll_kernel_type

    implicit none

    type( field_type ), intent( inout )          :: mr(nummr)
    type( field_type ), intent( in )             :: exner

    type( field_collection_type ), intent(in)    :: derived_fields
    type( field_collection_type ), intent(inout) :: convection_fields

    integer(kind=i_def), intent(in)              :: outer

    ! Temporary fields unpacked from collections
    type( field_type ), pointer :: exner_in_wth  => null()
    type( field_type ), pointer :: theta_star => null()
    type( field_type ), pointer :: dt_conv => null()
    type( field_type ), pointer :: dmv_conv => null()
    type( field_type ), pointer :: dmcl_conv => null()
    type( field_type ), pointer :: dcfl_conv => null()
    type( field_type ), pointer :: dbcf_conv => null()
    type( field_type ), pointer :: conv_rain => null()
    type( field_type ), pointer :: conv_snow => null()
    integer(tik)                :: id_alg, id_diag

    if ( LPROF ) call start_timing( id_alg, 'convection.alg' )

    call log_event( 'Running LL convection scheme', LOG_LEVEL_INFO )

    ! Unpack fields
    call derived_fields%get_field('exner_in_wth', exner_in_wth)
    call derived_fields%get_field('theta_star', theta_star)

    call convection_fields%get_field('dt_conv', dt_conv)
    call convection_fields%get_field('dmv_conv', dmv_conv)
    call convection_fields%get_field('dmcl_conv', dmcl_conv)
    call convection_fields%get_field('dcfl_conv', dcfl_conv)
    call convection_fields%get_field('dbcf_conv', dbcf_conv)
    call convection_fields%get_field('conv_rain', conv_rain)
    call convection_fields%get_field('conv_snow', conv_snow)

    call invoke(conv_ll_kernel_type(dt_conv, dmv_conv, dmcl_conv, dcfl_conv,  &
                                    theta_star, mr(imr_v), mr(imr_cl),        &
                                    exner_in_wth, exner, conv_rain),          &

                ! bulk fraction change is just the liquid change
                setval_X(dbcf_conv,dcfl_conv) )

    if ( LPROF ) call stop_timing( id_alg, 'convection.alg' )

    if (write_diag .and. use_xios_io .and. outer == outer_iterations) then
      if ( LPROF ) call start_timing( id_diag, 'diags.convection' )
      call dt_conv%write_field('convection__dt_conv')
      call dmv_conv%write_field('convection__dmv_conv')
      call conv_rain%write_field('convection__conv_rain')
      call conv_snow%write_field('convection__conv_snow')
      if ( LPROF ) call stop_timing( id_diag, 'diags.convection' )
    end if

  end subroutine conv_ll_alg

end module conv_ll_alg_mod
