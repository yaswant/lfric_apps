!-------------------------------------------------------------------------------
! (c) Crown copyright 2019 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-------------------------------------------------------------------------------
!> @brief Interface to the PC2 cloud scheme

module pc2_conv_coupling_alg_mod

   use constants_mod,        only: r_def
   use field_mod,            only: field_type
   use field_collection_mod, only: field_collection_type
   use io_config_mod,        only: write_diag, use_xios_io
   use mesh_mod,             only: mesh_type
   use mr_indices_mod,       only: nummr, imr_v, imr_cl, imr_ci, imr_s
   use um_sizes_init_mod,    only: um_sizes_init
   use microphysics_config_mod, only: microphysics_casim
   use convection_config_mod,only: cv_scheme, cv_scheme_comorph

   implicit none

   private
   public pc2_conv_coupling_alg

contains
  !>@brief Run pc2_conv_coupling
  !>@details Convection affects cloud condensate and cloud fraction.
  !>         Increments from detrainment get calculated by the convection
  !>         scheme itself.
  !>         Increments from subsidence advection, subsidence warming and
  !>         erosion get calculated downward from here.
  !>         Fields do not get updated. Increments are passed back out.
  !>         See UMDP30 for full scheme details.
  !>@param[in,out] mr               Mixing ratios
  !>@param[in]     derived_fields   Group of derived fields
  !>@param[in,out] convection_fields Fields for convection scheme
  !>@param[in,out] cloud_fields      Fields for cloud scheme
  !>@param[in]     dt                The model timestep length
  subroutine pc2_conv_coupling_alg( mr, derived_fields,                  &
                                    convection_fields, cloud_fields, dt )

    use constants_mod,                only: i_def
    use timing_mod,                   only: start_timing, stop_timing, &
                                            tik, LPROF
    use pc2_conv_coupling_kernel_mod, only: pc2_conv_coupling_kernel_type
    use casim_conv_inc_kernel_mod,    only: casim_conv_inc_kernel_type

    implicit none

    type( field_type ), intent( inout ) :: mr(nummr)

    type( field_collection_type ), intent(in)    :: derived_fields
    type( field_collection_type ), intent(inout) :: convection_fields
    type( field_collection_type ), intent(inout) :: cloud_fields
    real( kind=r_def ),            intent(in)    :: dt

    ! Temporary unpacked fields from collections
    type( field_type ), pointer :: exner_wth       => null()
    type( field_type ), pointer :: theta_star      => null()

    type( field_type ), pointer :: liquid_fraction => null()
    type( field_type ), pointer :: frozen_fraction => null()
    type( field_type ), pointer :: bulk_fraction   => null()

    type( field_type ), pointer :: dt_conv         => null()
    type( field_type ), pointer :: dmv_conv        => null()
    type( field_type ), pointer :: dmcl_conv       => null()
    type( field_type ), pointer :: dms_conv
    type( field_type ), pointer :: dcfl_conv       => null()
    type( field_type ), pointer :: dcff_conv       => null()
    type( field_type ), pointer :: dbcf_conv       => null()
    type( field_type ), pointer :: pressure_inc_env

    type(mesh_type), pointer :: mesh => null()
    integer(i_def) :: ncells
    integer(tik)   :: id_alg, id_diags

    if ( LPROF ) call start_timing( id_alg, 'cloud.pc2_conv' )

    call derived_fields%get_field('exner_in_wth', exner_wth)
    call derived_fields%get_field('theta_star', theta_star)

    call cloud_fields%get_field('liquid_fraction', liquid_fraction)
    call cloud_fields%get_field('frozen_fraction', frozen_fraction)
    call cloud_fields%get_field('bulk_fraction', bulk_fraction)

    call convection_fields%get_field('dt_conv', dt_conv)
    call convection_fields%get_field('dmv_conv', dmv_conv)
    call convection_fields%get_field('dmcl_conv', dmcl_conv)
    call convection_fields%get_field('dms_conv', dms_conv)
    call convection_fields%get_field('dcfl_conv', dcfl_conv)
    call convection_fields%get_field('dcff_conv', dcff_conv)
    call convection_fields%get_field('dbcf_conv', dbcf_conv)
    call convection_fields%get_field('pressure_inc_env', pressure_inc_env)

    ! Re-initialise UM to work on whole domain (i-first)
    mesh => exner_wth%get_mesh()
    ncells = mesh%get_last_edge_cell()
    call um_sizes_init(ncells)

    call invoke ( name="update_from_pc2_conv_coupling",            &
                                                                   !
                  pc2_conv_coupling_kernel_type ( theta_star,      &
                                                  mr(imr_v),       &
                                                  mr(imr_cl),      &
                                                  liquid_fraction, &
                                                  frozen_fraction, &
                                                  bulk_fraction,   &
                                                  exner_wth,       &
                                                  pressure_inc_env,&
                                                  dt_conv,         &
                                                  dmv_conv,        &
                                                  dmcl_conv,       &
                                                  dcfl_conv,       &
                                                  dcff_conv,       &
                                                  dbcf_conv, dt),  &
                                                                   !
                  ! All variables are being updated apart from theta
                  inc_X_plus_Y(mr(imr_v),       dmv_conv),         &
                  inc_X_plus_Y(mr(imr_cl),      dmcl_conv),        &
                  inc_X_plus_Y(liquid_fraction, dcfl_conv),        &
                  inc_X_plus_Y(frozen_fraction, dcff_conv),        &
                  inc_X_plus_Y(bulk_fraction,   dbcf_conv)         )

    ! Re-initialise UM back to columns
    call um_sizes_init(1_i_def)

    if (microphysics_casim .and. (.not. cv_scheme == cv_scheme_comorph)) then
      ! If Casim and WB, add any convective source to the ice cloud, but
      ! subtract any sink from the snow
      call invoke(inc_X_plus_Y(mr(imr_ci), dms_conv),              &
                  casim_conv_inc_kernel_type(mr(imr_ci), mr(imr_s)))
    else
      call invoke(inc_X_plus_Y(mr(imr_s), dms_conv))
    end if

    if ( LPROF ) call stop_timing( id_alg, 'cloud.pc2_conv' )

    if ( write_diag .and. use_xios_io ) then
      if ( LPROF ) call start_timing( id_diags, 'diags.pc2_conv' )
      call dt_conv%write_field('cloud__dt_pc2_conv_coupling')
      call dmv_conv%write_field('cloud__dmv_pc2_conv_coupling')
      call dmcl_conv%write_field('cloud__dmcl_pc2_conv_coupling')
      call dms_conv%write_field('cloud__dms_pc2_conv_coupling')
      call dcfl_conv%write_field('cloud__dcfl_pc2_conv_coupling')
      call dcff_conv%write_field('cloud__dcff_pc2_conv_coupling')
      call dbcf_conv%write_field('cloud__dbcf_pc2_conv_coupling')
      if ( LPROF ) call stop_timing( id_diags, 'diags.pc2_conv' )
    end if

  end subroutine pc2_conv_coupling_alg

end module pc2_conv_coupling_alg_mod
