!-------------------------------------------------------------------------------
! (c) Crown copyright 2017 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-------------------------------------------------------------------------------
!> @brief Interface to the PC2 cloud scheme

module pc2_initiation_alg_mod

   use constants_mod,           only: i_def, str_def
   use field_mod,               only: field_type
   use integer_field_mod,       only: integer_field_type
   use field_collection_mod,    only: field_collection_type
   use fs_continuity_mod,       only: Wtheta
   use sci_geometric_constants_mod,   &
                                only: get_height_fv
   use mesh_mod,                only: mesh_type
   use um_sizes_init_mod,       only: um_sizes_init
   use io_config_mod,           only: write_diag, use_xios_io

   implicit none

   private
   public pc2_initiation_alg

contains
  !>@brief Run pc2_initiation
  !>@details This algorithm calls the PC2 initiation kernel which assesses the
  !>         current model state fields. If certain conditions are met, the
  !>         kernel will create cloud from clear sky or break up an overcast
  !>         scene.
  !>
  !>         Moisture and Cloud fields are updated accordingly and
  !>         PC2 increments to potential temperature returned.
  !>
  !>         See UMDP30 for full scheme details.
  !>@param[in,out] mr                (in,out) Current Mixing ratios
  !>@param[in]     theta             (in)     Current Potential temperature
  !>@param[in]     exner_w3          (in)     Current value of enxer in w3 space
  !>@param[in]     exner_wth         (in)     Current value of exner in wtheta
  !>@param[in]     mr_n              (in)     Start of timestep mixing ratios
  !>@param[in]     theta_n           (in)     Start of timestep pot temp
  !>@param[in]     derived_fields    (in)     Group of derived fields
  !>@param[in]     turbulence_fields (in)     Fields for turbulence scheme
  !>@param[in]     cloud_fields      (in,out) Fields for cloud scheme
  !>@param[in,out] dtheta_pc2_inc    (in,out) Potential temperature response
  !>@param[in]     name_ext          (in)     Name for diagnostics
  subroutine pc2_initiation_alg( mr,                &
                                 theta,             &
                                 exner_w3,          &
                                 exner_wth,         &
                                 mr_n,              &
                                 theta_n,           &
                                 derived_fields,    &
                                 turbulence_fields, &
                                 cloud_fields,      &
                                 sskew_bm, svar_bm, &
                                 svar_tb,           &
                                 dtheta_pc2_inc, name_ext )

    use pc2_initiation_kernel_mod, only: pc2_initiation_kernel_type
    use mr_indices_mod,            only: imr_v, imr_cl, imr_ci, imr_s, nummr
    use timing_mod,                only: start_timing, stop_timing, tik, LPROF

    implicit none

    type( field_type ), intent( in )    :: theta
    type( field_type ), intent( in )    :: exner_w3
    type( field_type ), intent( inout ) :: mr   ( nummr )
    type( field_type ), intent( in )    :: mr_n ( nummr ), theta_n
    type( field_type ), intent( in )    :: exner_wth

    character(str_def), intent(in) :: name_ext

    type( field_collection_type ), intent(in) :: derived_fields
    type( field_collection_type ), intent(in) :: turbulence_fields
    type( field_collection_type ), intent(in) :: cloud_fields

    type( field_type ), intent(inout) :: sskew_bm, svar_bm, svar_tb
    type( field_type ), intent(inout) :: dtheta_pc2_inc
    type( field_type )                :: dmv_pc2_inc
    type( field_type )                :: dmcl_pc2_inc
    type( field_type )                :: dmci_pc2_inc
    type( field_type )                :: dms_pc2_inc
    type( field_type )                :: dcfl_pc2_inc
    type( field_type )                :: dcff_pc2_inc
    type( field_type )                :: dbcf_pc2_inc

    ! Temporary unpacked fields from collections
    type( field_type ), pointer :: exner_wth_n  => null()

    ! For PC2
    type( field_type ), pointer :: liquid_fraction => null()
    type( field_type ), pointer :: frozen_fraction => null()
    type( field_type ), pointer :: bulk_fraction   => null()
    type( field_type ), pointer :: area_fraction   => null()
    type( field_type ), pointer :: rh_crit         => null()

    type( field_type ), pointer :: dsldzm      => null()
    type( field_type ), pointer :: mix_len_bm  => null()
    type( field_type ), pointer :: wvar        => null()
    type( field_type ), pointer :: gradrinr    => null()
    type( field_type ), pointer :: zh          => null()
    type( field_type ), pointer :: zhsc        => null()
    type( field_type ), pointer :: inv_depth   => null()
    type( integer_field_type ), pointer :: bl_type_ind => null()
    type( field_type ), pointer :: tau_dec_bm  => null()
    type( field_type ), pointer :: tau_hom_bm  => null()
    type( field_type ), pointer :: tau_mph_bm  => null()

    type( field_type ), pointer :: zlcl_mixed      => null()
    type( integer_field_type ), pointer :: cumulus => null()

    type( field_type ), pointer :: height_wth => null()

    type( mesh_type ),  pointer :: mesh => null()
    integer( kind=i_def ) :: ncells
    integer( tik )        :: id_alg, id_diags

    if ( LPROF ) call start_timing( id_alg, 'cloud.pc2_initiation' )

    ! Unpack fields
    call derived_fields%get_field('exner_wth_n', exner_wth_n)

    call cloud_fields%get_field('bulk_fraction', bulk_fraction)
    call cloud_fields%get_field('liquid_fraction', liquid_fraction)
    call cloud_fields%get_field('frozen_fraction', frozen_fraction)
    call cloud_fields%get_field('area_fraction', area_fraction)
    call cloud_fields%get_field('rh_crit', rh_crit)

    call cloud_fields%get_field('tau_dec_bm', tau_dec_bm)
    call cloud_fields%get_field('tau_hom_bm', tau_hom_bm)
    call cloud_fields%get_field('tau_mph_bm', tau_mph_bm)

    call turbulence_fields%get_field('z_lcl', zlcl_mixed)
    call turbulence_fields%get_field('cumulus', cumulus)

    call turbulence_fields%get_field('dsldzm', dsldzm)
    call turbulence_fields%get_field('mix_len_bm', mix_len_bm)
    call turbulence_fields%get_field('wvar', wvar)
    call turbulence_fields%get_field('gradrinr', gradrinr)
    call turbulence_fields%get_field('zh', zh)
    call turbulence_fields%get_field('zhsc', zhsc)
    call turbulence_fields%get_field('inv_depth', inv_depth)
    call turbulence_fields%get_field('bl_type_ind', bl_type_ind)

    mesh       => theta%get_mesh()
    height_wth => get_height_fv(Wtheta, mesh%get_id())

    call theta%copy_field_properties(dmv_pc2_inc)
    call theta%copy_field_properties(dmcl_pc2_inc)
    call theta%copy_field_properties(dmci_pc2_inc)
    call theta%copy_field_properties(dms_pc2_inc)
    call theta%copy_field_properties(dcfl_pc2_inc)
    call theta%copy_field_properties(dcff_pc2_inc)
    call theta%copy_field_properties(dbcf_pc2_inc)

    ncells = mesh%get_last_edge_cell()
    call um_sizes_init(ncells)

    call invoke( name="update_from_pc2_initiation",              &
                 pc2_initiation_kernel_type ( mr(imr_v),         &
                                              mr(imr_cl),        &
                                              mr(imr_ci),        &
                                              mr(imr_s),         &
                                              liquid_fraction,   &
                                              frozen_fraction,   &
                                              bulk_fraction,     &
                                              theta,             &
                                              exner_wth,         &
                                              exner_w3,          &
                                              dsldzm,            &
                                              mix_len_bm,        &
                                              wvar,              &
                                              gradrinr,          &
                                              zh,                &
                                              zhsc,              &
                                              inv_depth,         &
                                              bl_type_ind,       &
                                              tau_dec_bm,        &
                                              tau_hom_bm,        &
                                              tau_mph_bm,        &
                      ! Next 4 are start of timestep mv, mcl, theta
                      ! and exner to find RHt at start of timestep
                                              mr_n(imr_v),       &
                                              mr_n(imr_cl),      &
                                              theta_n,           &
                                              exner_wth_n,       &
                                              zlcl_mixed,        &
                                              cumulus,           &
                                              height_wth,        &
                                                ! Responses
                                              dtheta_pc2_inc,    &
                                              dmv_pc2_inc,       &
                                              dmcl_pc2_inc,      &
                                              dmci_pc2_inc,      &
                                              dms_pc2_inc,       &
                                              dcfl_pc2_inc,      &
                                              dcff_pc2_inc,      &
                                              dbcf_pc2_inc,      &
                                              sskew_bm,          &
                                              svar_bm,           &
                                              svar_tb,           &
                                              rh_crit       ) ,  &
                                                                 !
                ! Update all field (apart from theta)
                ! with increments from initiation
                                                                 !
                inc_X_plus_Y(mr(imr_v),       dmv_pc2_inc),      &
                inc_X_plus_Y(mr(imr_cl),      dmcl_pc2_inc),     &
                inc_X_plus_Y(mr(imr_ci),      dmci_pc2_inc),     &
                inc_X_plus_Y(mr(imr_s),       dms_pc2_inc),      &
                inc_X_plus_Y(liquid_fraction, dcfl_pc2_inc),     &
                inc_X_plus_Y(frozen_fraction, dcff_pc2_inc),     &
                inc_X_plus_Y(bulk_fraction,   dbcf_pc2_inc),     &
                ! area_fraction = bulk_fraction
                setval_X    (area_fraction,   bulk_fraction)     )

    call um_sizes_init(1_i_def)

    if ( LPROF ) call stop_timing( id_alg, 'cloud.pc2_initiation' )

    if ( write_diag .and. use_xios_io ) then
      if ( LPROF ) call start_timing( id_diags, 'diags.pc2_initiation' )
      call dtheta_pc2_inc%write_field('cloud__dtheta_pc2_initiation'//trim(name_ext))
      call dmv_pc2_inc%write_field('cloud__dmv_pc2_initiation'//trim(name_ext))
      call dmcl_pc2_inc%write_field('cloud__dmcl_pc2_initiation'//trim(name_ext))
      call dmci_pc2_inc%write_field('cloud__dmci_pc2_initiation'//trim(name_ext))
      call dms_pc2_inc%write_field('cloud__dms_pc2_initiation'//trim(name_ext))
      call dcfl_pc2_inc%write_field('cloud__dcfl_pc2_initiation'//trim(name_ext))
      call dcff_pc2_inc%write_field('cloud__dcff_pc2_initiation'//trim(name_ext))
      call dbcf_pc2_inc%write_field('cloud__dbcf_pc2_initiation'//trim(name_ext))
      if ( LPROF ) call stop_timing( id_diags, 'diags.pc2_initiation' )
    end if

    nullify( mesh )

  end subroutine pc2_initiation_alg

end module pc2_initiation_alg_mod
