!-------------------------------------------------------------------------------
! (c) Crown copyright 2023 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-------------------------------------------------------------------------------
!> @brief Interface to the main UM Electric (lightning) scheme call

module electric_main_alg_mod

  use field_mod,            only: field_type
  use field_collection_mod, only: field_collection_type
  use mesh_mod,             only: mesh_type
  use io_config_mod,        only: write_diag, use_xios_io

  use constants_mod,        only: r_def, i_def

  use sci_geometric_constants_mod, only: get_dz_at_wtheta

  use electric_main_diags_mod, only: initialise_main_diags_for_electric,       &
                                     output_main_diags_for_electric

  implicit none

  private
  public electric_main_alg

contains
  !>@brief
  !>@details
  !>
  !>@param[in]
  !>@param[in,out]
  subroutine electric_main_alg( mr_n, theta, derived_fields, electric_fields,  &
                                dt )

    use electric_main_kernel_mod,  only: electric_main_kernel_type
    use mr_indices_mod,            only: imr_ci, imr_s, imr_g, nummr
    use timing_mod,                only: start_timing, stop_timing, tik, LPROF
    use log_mod,                   only: log_event, LOG_LEVEL_INFO

    implicit none

    type( field_type ), intent( in ) :: mr_n ( nummr ), theta
    type( field_collection_type ), intent(in) :: derived_fields
    type( field_collection_type ), intent(in) :: electric_fields
    real( kind=r_def ),            intent(in) :: dt

    ! Local variables
    type( field_type ) :: rhodz_dry
    type( field_type ) :: t_local

    ! Diagnostic variables as type ( field_type )
    type( field_type ) :: total_flash_rate_2d
    type( field_type ) :: storm_field_2d
    type( field_type ) :: fr1_mc_2d
    type( field_type ) :: fr2_mc_2d
    type( field_type ) :: gwp_2d
    type( field_type ) :: tiwp_2d
    type( field_type ) :: num_flashes_2d

    ! Temporary unpacked fields from collections
    type( field_type ), pointer :: rho_in_wth      => null()
    type( field_type ), pointer :: dz_in_wth       => null()
    type( field_type ), pointer :: exner_in_wth    => null()
    type( field_type ), pointer :: w_in_wth        => null()
    type( field_type ), pointer :: flash_potential => null()

    type(mesh_type), pointer :: mesh => null()

    integer(tik)  :: id

    !--------------------------------------------------------------------------
    ! End of declarations and start of the subroutine actually doing something
    !--------------------------------------------------------------------------
    if ( LPROF ) call start_timing( id, 'electric' )

    call log_event( 'slow_physics: Running Lightning Scheme', LOG_LEVEL_INFO )

    mesh => theta%get_mesh()

    ! Unpack fields
    call derived_fields%get_field('exner_in_wth', exner_in_wth)
    call derived_fields%get_field('rho_in_wth', rho_in_wth)
    call derived_fields%get_field('w_in_wth', w_in_wth)

    ! Set up rhodz_dry and t_local arrays and initialise to zero.
    call rho_in_wth%copy_field_properties(rhodz_dry)
    call theta%copy_field_properties(t_local)
    call invoke( setval_c(rhodz_dry, 0.0_r_def), &
                 setval_c(t_local,   0.0_r_def))

    dz_in_wth => get_dz_at_wtheta( mesh%get_id() )

    call electric_fields%get_field('flash_potential', flash_potential)
    call flash_potential%copy_field_properties(num_flashes_2d)

    call initialise_main_diags_for_electric( gwp_2d, tiwp_2d,                  &
                                             total_flash_rate_2d,              &
                                             fr1_mc_2d, fr2_mc_2d,             &
                                             storm_field_2d)

    ! multiply layer thickness by wet/dry rho before calling scheme.

    call invoke( X_times_Y(rhodz_dry, rho_in_wth, dz_in_wth ),                 &
                 X_times_Y(t_local, theta, exner_in_wth),                      &
                 electric_main_kernel_type ( mr_n(imr_ci), mr_n(imr_s),        &
                                             mr_n(imr_g), t_local,             &
                                             w_in_wth, rhodz_dry, dt,          &
                                             flash_potential, num_flashes_2d,  &
                                             total_flash_rate_2d,              &
                                             storm_field_2d,                   &
                                             fr1_mc_2d, fr2_mc_2d, gwp_2d,     &
                                             tiwp_2d ) )

    if ( LPROF ) call stop_timing( id, 'electric' )

    !---------------------------
    ! Output Diagnostics
    !---------------------------

    if ( write_diag .and. use_xios_io ) then

      call output_main_diags_for_electric( gwp_2d, tiwp_2d,                    &
                                           total_flash_rate_2d,                &
                                           num_flashes_2d,                     &
                                           fr1_mc_2d, fr2_mc_2d,               &
                                           storm_field_2d,                     &
                                           flash_potential )

    end if

    nullify( mesh )
  end subroutine electric_main_alg
end module electric_main_alg_mod
