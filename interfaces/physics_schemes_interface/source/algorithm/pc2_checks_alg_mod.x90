!-------------------------------------------------------------------------------
! (c) Crown copyright 2017 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-------------------------------------------------------------------------------
!> @brief Interface to the PC2 cloud scheme

module pc2_checks_alg_mod

   use field_mod,            only: field_type
   use field_collection_mod, only: field_collection_type
   use io_config_mod,        only: write_diag, use_xios_io

   implicit none

   private
   public pc2_checks_alg

contains
  !>@brief Run pc2_checks
  !>@details PC2 checks carried out after slow physics before advection
  !>         1) slow physics has calculated increment from microphysics
  !>            and radiation, these are calculated in parallel unaware
  !>            of each other.
  !>         2) Once both sets of increments get added, it is worth
  !>            checking that clouds fields are sensible and self-consistent.
  !>         3) Any adjustments get applied via increments.
  !>         4) Fields do not get updated. Increments are passed back out.
  !>         See UMDP30 for full scheme details.
  !>@param[in]     mr               Start of timestep mixing ratios
  !>@param[in]     theta            Potential temperature (theta) in wth space
  !>@param[in]     derived_fields   Group of derived fields
  !>@param[in]     cloud_fields     Group of cloud fields
  !>@param[out]    dtheta_pc2_inc   Potential temperature response in wth space
  !>@param[out]    dmv_pc2_inc      Vapour                response in wth space
  !>@param[out]    dmcl_pc2_inc     Liquid water content  response in wth space
  !>@param[out]    dmci_pc2_inc     Ice water content     response in wth space
  !>@param[out]    dcfl_pc2_inc     Liquid cloud fraction response in wth space
  !>@param[out]    dcff_pc2_inc     Frozen cloud fraction response in wth space
  !>@param[out]    dbcf_pc2_inc     Bulk cloud fraction   response in wth space

  subroutine pc2_checks_alg( mr,             &
                             theta,          &
                             derived_fields, &
                             cloud_fields,   &
                             dtheta_pc2_inc, &
                             dmv_pc2_inc,    &
                             dmcl_pc2_inc,   &
                             dmci_pc2_inc,   &
                             dms_pc2_inc,    &
                             dcfl_pc2_inc,   &
                             dcff_pc2_inc,   &
                             dbcf_pc2_inc    )

    use pc2_checks_kernel_mod, only: pc2_checks_kernel_type
    use mr_indices_mod,        only: imr_v, imr_cl, imr_ci, nummr, imr_s
    use timing_mod,            only: start_timing, stop_timing, tik, LPROF

    implicit none

    type( field_type ), intent( in ) :: mr ( nummr )
    type( field_type ), intent( in ) :: theta

    type( field_collection_type ), intent(in) :: derived_fields, cloud_fields

    type( field_type ), intent(out) :: dtheta_pc2_inc
    type( field_type ), intent(out) :: dmv_pc2_inc
    type( field_type ), intent(out) :: dmcl_pc2_inc
    type( field_type ), intent(out) :: dmci_pc2_inc
    type( field_type ), intent(out) :: dms_pc2_inc
    type( field_type ), intent(out) :: dcfl_pc2_inc
    type( field_type ), intent(out) :: dcff_pc2_inc
    type( field_type ), intent(out) :: dbcf_pc2_inc

    ! Temporary unpacked fields from collections
    type( field_type ), pointer :: exner_in_wth  => null()

    ! For PC2
    type( field_type ), pointer :: liquid_fraction => null()
    type( field_type ), pointer :: frozen_fraction => null()
    type( field_type ), pointer :: bulk_fraction   => null()
    integer( tik )              :: id_alg, id_diags

    if ( LPROF ) call start_timing( id_alg, 'cloud.pc2_checks' )

    ! Unpack fields
    call derived_fields%get_field('exner_in_wth', exner_in_wth)

    call cloud_fields%get_field('bulk_fraction', bulk_fraction)
    call cloud_fields%get_field('liquid_fraction', liquid_fraction)
    call cloud_fields%get_field('frozen_fraction', frozen_fraction)

    call theta%copy_field_properties(dtheta_pc2_inc)
    call theta%copy_field_properties(dmv_pc2_inc)
    call theta%copy_field_properties(dmcl_pc2_inc)
    call theta%copy_field_properties(dmci_pc2_inc)
    call theta%copy_field_properties(dms_pc2_inc)
    call theta%copy_field_properties(dcfl_pc2_inc)
    call theta%copy_field_properties(dcff_pc2_inc)
    call theta%copy_field_properties(dbcf_pc2_inc)

    call invoke ( pc2_checks_kernel_type ( mr(imr_v),           & ! IN, they do
                                           mr(imr_cl),          & ! IN, not get
                                           mr(imr_ci),          & ! IN, updated
                                           mr(imr_s),           &
                                           liquid_fraction,     &
                                           frozen_fraction,     &
                                           bulk_fraction,       &
                                           theta,               &
                                           exner_in_wth,        &
                                                      ! Responses
                                           dtheta_pc2_inc,      &
                                           dmv_pc2_inc,         &
                                           dmcl_pc2_inc,        &
                                           dmci_pc2_inc,        &
                                           dms_pc2_inc,         &
                                           dcfl_pc2_inc,        &
                                           dcff_pc2_inc,        &
                                           dbcf_pc2_inc ) )

    if ( LPROF ) call stop_timing( id_alg, 'cloud.pc2_checks' )

    if ( write_diag .and. use_xios_io ) then
      if ( LPROF ) call start_timing( id_diags, 'diags.pc2_checks' )
      call dtheta_pc2_inc%write_field('cloud__dtheta_pc2_checks')
      call dmv_pc2_inc%write_field('cloud__dmv_pc2_checks')
      call dmcl_pc2_inc%write_field('cloud__dmcl_pc2_checks')
      call dmci_pc2_inc%write_field('cloud__dmci_pc2_checks')
      call dms_pc2_inc%write_field('cloud__dms_pc2_checks')
      call dcfl_pc2_inc%write_field('cloud__dcfl_pc2_checks')
      call dcff_pc2_inc%write_field('cloud__dcff_pc2_checks')
      call dbcf_pc2_inc%write_field('cloud__dbcf_pc2_checks')
      if ( LPROF ) call stop_timing( id_diags, 'diags.pc2_checks' )
    end if

  end subroutine pc2_checks_alg

end module pc2_checks_alg_mod
